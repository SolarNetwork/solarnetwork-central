plugins {
    id 'java-library'
    id 'eclipse'
	id 'org.springframework.boot' version '3.4.3' apply false
}

apply plugin: 'java-library'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'
apply plugin: 'signing'

dependencyManagement {
	imports {
		mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}
}

description = 'SolarNet: Common'
version = '2.27.0'

base {
	archivesName = 'solarnet-common'
}

configurations {
	all*.exclude group: 'commons-logging'
	all*.exclude group: 'javax.servlet'
	all*.exclude group: 'log4j', module: 'log4j'
	all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

dependencies {
	// Spring
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:${myBatisStarterVersion}"
	runtimeOnly 'org.springframework.boot:spring-boot-properties-migrator'

	implementation 'org.springframework.security:spring-security-core'
	implementation 'org.springframework.security:spring-security-oauth2-client'
	implementation 'org.springframework:spring-web'
	
	// DAO
	implementation 'org.mybatis:mybatis'
	implementation 'org.mybatis:mybatis-spring'
	runtimeOnly 'org.postgresql:postgresql'
	
	// JAXB
	implementation 'jakarta.xml.bind:jakarta.xml.bind-api'
	implementation 'org.glassfish.jaxb:jaxb-runtime'
	
	// JCache
	implementation 'javax.cache:cache-api'
	implementation 'org.ehcache:ehcache'

	// JSON
	implementation 'com.fasterxml.jackson.core:jackson-annotations'
	implementation 'com.fasterxml.jackson.core:jackson-core'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	
	// Netty
	implementation 'io.netty:netty-buffer'
	implementation 'io.netty:netty-codec'
	implementation 'io.netty:netty-codec-stomp'
	implementation 'io.netty:netty-common'
	implementation 'io.netty:netty-handler'
	implementation 'io.netty:netty-resolver'
	implementation 'io.netty:netty-transport'

	// Utilities
	implementation "software.amazon.awssdk:ec2:${awsSdk2Version}"
	implementation "software.amazon.awssdk:s3:${awsSdk2Version}"
	implementation "software.amazon.awssdk:secretsmanager:${awsSdk2Version}"
	implementation "com.cronutils:cron-utils:${cronUtilsVersion}"
	implementation "com.fasterxml.uuid:java-uuid-generator:${uuidGeneratorVersion}"
	implementation 'com.nimbusds:nimbus-jose-jwt:[9.37,10)'
	implementation 'commons-codec:commons-codec'
	implementation "commons-io:commons-io:${commonsIoVersion}"
	implementation 'io.github.classgraph:classgraph:4.8.179'
	implementation 'jakarta.validation:jakarta.validation-api'
	implementation 'jakarta.annotation:jakarta.annotation-api'
	implementation "net.sf.supercsv:super-csv:${superCsvVersion}"
	implementation 'org.apache.commons:commons-lang3'
	implementation 'org.apache.commons:commons-text:1.13.0'
	implementation 'org.aspectj:aspectjweaver'
	implementation 'org.slf4j:slf4j-api'
	
	
	// Swagger support
	implementation "io.swagger.core.v3:swagger-annotations-jakarta:${swaggerVersion}"
	implementation "io.swagger.core.v3:swagger-core-jakarta:${swaggerVersion}"
	implementation "io.swagger.core.v3:swagger-models-jakarta:${swaggerVersion}"
	
	// Not pushed to extending projects by default
	compileOnly "net.sf.saxon:Saxon-HE:${saxonVersion}"
	
	// OSGi Compatibility
	implementation 'org.osgi:org.osgi.enterprise:5.0.0'
	
	// SolarNetwork
	api 'commons-beanutils:commons-beanutils:1.10.0'
	api "com.cronutils:cron-utils:${cronUtilsVersion}"
	api 'org.apache.commons:commons-compress:1.27.1'
	api "net.solarnetwork.common:net.solarnetwork.common:${snCommonVersion}"
	api "net.solarnetwork.common:net.solarnetwork.common.mqtt:${snCommonMqttVersion}"
	api "net.solarnetwork.common:net.solarnetwork.common.pki.bc:${snCommonPkiBcVersion}"
	api "net.solarnetwork.common:net.solarnetwork.common.web.jakarta:${snCommonWebJakartaVersion}"
	
	// Web
	implementation 'jakarta.servlet:jakarta.servlet-api'
	
	// Testing
	testImplementation project(':solarnet-common-test')
    testImplementation "org.mybatis.spring.boot:mybatis-spring-boot-starter-test:${myBatisStarterVersion}"
    testImplementation "org.threeten:threeten-extra:${threetenExtraVersion}"
    testImplementation "net.sf.saxon:Saxon-HE:${saxonVersion}"
    
}

jar.manifest {
    attributes('Implementation-Version': version)
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			pom {
				name = project.description
				description = 'Shared library for all SolarNet projects.'
				url = 'https://github.com/SolarNetwork/solarnetwork-central'
				licenses {
					license {
						name = 'GNU General Public License, Version 2.0'
						url = 'https://opensource.org/licenses/GPL-2.0'
					}
				}
				developers {
					developer {
						name = 'SolarNetwork Developers'
						email = 'code@solarnetwork.net'
						organization = 'SolarNetwork Foundation'
						organizationUrl = 'https://solarnetwork.net'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/SolarNetwork/solarnetwork-central.git'
					developerConnection = 'scm:git:git@github.com:SolarNetwork/solarnetwork-central.git'
					url = 'https://github.com/SolarNetwork/solarnetwork-central'
				}
			}
		}
	}
	repositories {
		maven {
			name = "OSSRH"
			url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
			credentials {
				username = ossrhUsername
				password = ossrhPassword
			}
		}
	}
}

signing {
	useGpgCmd()
	sign publishing.publications.mavenJava
}
