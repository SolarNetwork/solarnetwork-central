plugins {
    id 'java-library'
    id 'eclipse'
	id 'org.springframework.boot' version '3.3.5' apply false
}

apply plugin: 'java-library'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'
apply plugin: 'signing'

dependencyManagement {
	imports {
		mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}
}

description = 'SolarNet: Commons Multipart'
version = '1.1.0'

base {
	archivesName = 'solarnet-commons-multipart'
}

configurations {
	all*.exclude group: 'commons-logging'
	all*.exclude group: 'javax.servlet'
	all*.exclude group: 'log4j', module: 'log4j'
	all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

dependencies {
	// Spring
	implementation 'org.springframework:spring-web'
	
	// Explicit Multipart support using Tomcat's implementation
	implementation 'org.apache.tomcat.embed:tomcat-embed-core'
	
	// Util
	implementation 'commons-codec:commons-codec'
	
	// Web
	implementation 'jakarta.servlet:jakarta.servlet-api'
	
	// Testing
	testImplementation 'org.apache.httpcomponents.client5:httpclient5'
	testImplementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

jar.manifest {
    attributes('Implementation-Version': version)
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			pom {
				name = project.description
				description = 'Port of Spring 5 Commons Multipart resolver.'
				url = 'https://github.com/SolarNetwork/solarnetwork-central'
				licenses {
					license {
						name = 'GNU General Public License, Version 2.0'
						url = 'https://opensource.org/licenses/GPL-2.0'
					}
				}
				developers {
					developer {
						name = 'SolarNetwork Developers'
						email = 'code@solarnetwork.net'
						organization = 'SolarNetwork Foundation'
						organizationUrl = 'https://solarnetwork.net'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/SolarNetwork/solarnetwork-central.git'
					developerConnection = 'scm:git:git@github.com:SolarNetwork/solarnetwork-central.git'
					url = 'https://github.com/SolarNetwork/solarnetwork-central'
				}
			}
		}
	}
	repositories {
		maven {
			name = "OSSRH"
			url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
			credentials {
				username = ossrhUsername
				password = ossrhPassword
			}
		}
	}
}

signing {
	useGpgCmd()
	sign publishing.publications.mavenJava
}
