plugins {
	id 'java'
    id 'eclipse'
	id 'org.springframework.boot' version '4.0.1'
	id 'org.graalvm.buildtools.native' version '0.11.1'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.graalvm.buildtools.native'

description = 'SolarFlux VerneMQ Webhook'
//version = '5.0.0' see gradle.properties

dependencies {
	implementation project(':solarnet-common')
	implementation project(':solarnet-common-web')

	implementation "org.springframework.boot:spring-boot-starter"
	implementation "org.springframework.boot:spring-boot-starter-cache"
	implementation "org.springframework.boot:spring-boot-starter-jdbc"
	implementation "org.springframework.boot:spring-boot-starter-webmvc"

	// DB
	runtimeOnly 'org.postgresql:postgresql'
		
	// JCache
	implementation 'javax.cache:cache-api'
	implementation 'org.ehcache:ehcache'

	// JSON
	implementation "com.fasterxml.jackson.core:jackson-annotations"
	implementation "tools.jackson.core:jackson-core"
	implementation "tools.jackson.core:jackson-databind"

	// Utils
    implementation 'com.github.veqryn:cidr-ip-trie:1.0.1'
    
    // Testing
	testImplementation project(':solarnet-common-test')
	testImplementation "org.springframework.boot:spring-boot-starter-jdbc-test"
	testImplementation "org.springframework.boot:spring-boot-starter-webmvc-test"
}

configurations.configureEach {
	exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-joda'
	exclude group: 'io.netty'
	exclude group: 'net.solarnetwork.common', module: 'net.solarnetwork.common.mqtt'
	exclude group: 'net.solarnetwork.common', module: 'net.solarnetwork.common.pki.bc'
	exclude group: 'org.apache.commons', module: 'commons-compress'
	exclude group: 'org.eclipse.virgo.mirrored'
	exclude group: 'org.mybatis'
	exclude group: 'org.mybatis.spring.boot', module: 'mybatis-spring-boot-starter'
	exclude group: 'org.osgi'
	exclude group: 'org.osgi.core'
	exclude group: 'org.osgi.enterprise'
	exclude group: 'org.springframework', module: 'spring-messaging'
	exclude group: 'org.springframework', module: 'spring-websocket'
	exclude group: 'org.springframework.security'
	exclude group: 'org.springframework.boot', module: 'spring-boot-security'
	exclude group: 'org.springframework.boot', module: 'spring-boot-security-test'
	exclude group: 'software.amazon.awssdk'
}

def commonManifest = {
    attributes(
        'Implementation-Title': 'SolarFlux VerneMQ Webhook Server',
		'Implementation-Version': project.version
    )
}

jar {
	manifest commonManifest
}

bootJar {
	manifest commonManifest
}

graalvmNative {
	binaries.main {
		def allArgs = []

		// NATIVE_BUILD_ARGS=a,b,c ../gradlew nativeCompile
        providers.environmentVariable('NATIVE_BUILD_ARGS')
            .orNull?.split(',')?.each { allArgs << it }

		// pass -PnativeBuildArgs=a,b,c
        providers.gradleProperty('nativeBuildArgs')
            .orNull?.split(',')?.each { allArgs << it }

		// pass -PnativeBuildArgsFile=dev.args, or default to native-build.args
		def argsFile = providers.gradleProperty('nativeBuildArgsFile')
                .map { file(it) }
                .orElse(file('native-build.args'))
                .get()
        
        if (argsFile.exists()) {
            allArgs.addAll(
                argsFile.readLines()
                    .findAll { it && !it.startsWith('#') }
            )
        }

		if (allArgs.isEmpty()) {
			buildArgs.addAll(['--enable-native-access=ALL-UNNAMED', '-O3', '-R:MaxHeapSize=128m', '-R:MinHeapSize=128m'])
		} else {
	        buildArgs.addAll(allArgs)
	    }
	}
	
	binaries.main {
		imageName = 'solarflux-webhook'
	}
}

tasks.withType(org.springframework.boot.gradle.tasks.aot.ProcessAot).configureEach {
    args("${aotProcessArgs}")
}
