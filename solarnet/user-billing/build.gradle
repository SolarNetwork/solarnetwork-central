plugins {
    id 'java-library'
    id 'eclipse'
	id 'org.springframework.boot' version '3.5.0' apply false
}

apply plugin: 'java-library'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'
apply plugin: 'signing'

dependencyManagement {
	imports {
		mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}
}

description = 'SolarNet: User Billing'
version = '2.6.0'

base {
	archivesName = 'solarnet-user-billing'
}

configurations {
	all*.exclude group: 'log4j', module: 'log4j'
	all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
	all*.exclude group: 'xml-apis', module: 'xml-apis'
}

dependencies {
	implementation project(':solarnet-common')
	implementation project(':solarnet-datum')
	implementation project(':solarnet-user')

	// Spring
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:${myBatisStarterVersion}"
	
	// DAO
	implementation 'org.mybatis:mybatis'
	implementation 'org.mybatis:mybatis-spring'
	runtimeOnly "org.postgresql:postgresql"
	
	// JCache
	implementation 'javax.cache:cache-api'
	implementation 'org.ehcache:ehcache'

	// JSON
	implementation 'com.fasterxml.jackson.core:jackson-annotations'
	implementation 'com.fasterxml.jackson.core:jackson-core'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	
	// PDF Support
	implementation libraries.batik
	
	// PDF
	implementation 'com.openhtmltopdf:openhtmltopdf-pdfbox:1.0.10'
	implementation 'com.openhtmltopdf:openhtmltopdf-slf4j:1.0.10'
	implementation 'com.openhtmltopdf:openhtmltopdf-svg-support:1.0.10'
	
	
	// Utilities
	implementation "net.sf.supercsv:super-csv:${superCsvVersion}"
	implementation "org.jsoup:jsoup:${jsoupVersion}"
	implementation 'org.slf4j:slf4j-api'
	
	// OSGi Compatibility
	implementation 'org.osgi:org.osgi.enterprise:5.0.0'
	
	// SolarNetwork
	implementation 'net.solarnetwork.common:net.solarnetwork.common.tmpl.st4:2.0.0'
	
	// Testing
	testImplementation project(':solarnet-common-test')
    testImplementation "org.mybatis.spring.boot:mybatis-spring-boot-starter-test:${myBatisStarterVersion}"
}

jar.manifest {
    attributes('Implementation-Version': version)
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			pom {
				name = project.description
				description = 'Shared library for all SolarNet projects.'
				url = 'https://github.com/SolarNetwork/solarnetwork-central'
				licenses {
					license {
						name = 'GNU General Public License, Version 2.0'
						url = 'https://opensource.org/licenses/GPL-2.0'
					}
				}
				developers {
					developer {
						name = 'SolarNetwork Developers'
						email = 'code@solarnetwork.net'
						organization = 'SolarNetwork Foundation'
						organizationUrl = 'https://solarnetwork.net'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/SolarNetwork/solarnetwork-central.git'
					developerConnection = 'scm:git:git@github.com:SolarNetwork/solarnetwork-central.git'
					url = 'https://github.com/SolarNetwork/solarnetwork-central'
				}
			}
		}
	}
	repositories {
		maven {
			name = "OSSRH"
			url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
			credentials {
				username = ossrhUsername
				password = ossrhPassword
			}
		}
	}
}

signing {
	useGpgCmd()
	sign publishing.publications.mavenJava
}
